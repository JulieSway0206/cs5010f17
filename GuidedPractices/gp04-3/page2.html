<!DOCTYPE html>
<html lang="en">

<head>

<meta charset="UTF-8">
<meta name="description" content="Northeastern University CS 5010">
<meta name="author" content="William D Clinger">
<!-- meta name="keywords" content="software" -->

<title>
  CS 5010: Guided Practice 4.3 &mdash; Solution
</title>

<link type="text/css"
      rel="stylesheet"
      href="../../cs5010.css" />

</head>

<body>

<!-- start of specific content -->

<div id="maincontent">
  <h2>
    CS 5010: Guided Practice 4.3 &mdash; Solution
  </h2>
  <p>
    Here is one solution:
  </p>
  <pre>
    ;; inventory-total-value : LOB -&gt; Number
    ;; GIVEN: a LOB
    ;; RETURNS: the value of all the copies on hand of all the books in the
    ;; given LOB
    ;; (inventory-total-value lob1) = 390
    
    (begin-for-test
      (check-equal? 
        (inventory-total-value empty)
        0
        "value of the empty inventory should have been 0")
      (check-equal?
        (inventory-total-value lob1)
        390
        "simple test"))
    
    ;; STRATEGY: Use template for LOB on books
    (define (inventory-total-value books)
      (cond [(empty? books) 0]
            [else (+ (book-inventory-value (first books))
                     (inventory-total-value (rest books)))]))
  </pre>
  <pre>
    ;; books-out-of-stock : LOB -&gt; LOB
    ;; GIVEN: a list of books
    ;; RETURNS: a list of the books that are out of stock in the given LOB
    ;; Example:
    ;; (books-out-of-stock lob1) =
    ;;  (list
    ;;    (make-book "Shakespeare" "Hamlet" 0 2)
    ;;    (make-book "Shakespeare" "Macbeth" 0 10))
    ;; STRATEGY: Use template for LOB on books
    
    (define (books-out-of-stock books)
      (cond [(empty? books) empty]
            [else (if (book-out-of-stock? (first books))
                      (cons (first books) (books-out-of-stock (rest books)))
                      (books-out-of-stock (rest books)))]))
    
    (check-equal?
      (books-out-of-stock lob1)
      (list
        (make-book "Shakespeare" "Hamlet" 0 2)
        (make-book "Shakespeare" "Macbeth" 0 10)))
    
    ;; book-out-of-stock? : Book -&gt; Boolean
    ;; returns true iff the given book is out of stock
    ;; EXAMPLE: 
    ;; (book-out-of-stock? (make-book "Felleisen" "HtDP/1" 20 7)) = false
    ;; (book-out-of-stock? (make-book "Felleisen" "HtDP/1"  0 7)) = true
    ;; STRATEGY: Use template for Book on b
    (define (book-out-of-stock? b)
      (&lt;= (book-on-hand b) 0))
    
    (begin-for-test
      (check-false
        (book-out-of-stock? (make-book "Felleisen" "HtDP/1" 20 7)))
      (check-true
        (book-out-of-stock? (make-book "Felleisen" "HtDP/1"  0 7))))
    
    ;; (check-equal? ... true) would have been fine, too.  Look at the
    ;; Help Desk page for check-equal? to see some other useful checks.
  </pre>
  <p>
    Note that the design of the help function
    <code>book-out-of-stock?</code>, with its deliverables,
    was needed to make this a good solution.
    Writing
  </p>
  <pre>
    (if (&lt;= (book-on-hand (first lob)) 0) ... ...)
  </pre>
  <p>
    matches the template, but is undesirable because it violates
    the principle of one-function-one-task, by forcing
    <code>books-out-of-stock</code> to know about both the
    representation of an inventory AND how being out of stock
    is represented for a book.
  </p>
</div>

<!-- end of specific content -->

<p class="muchsmaller">
For debugging: 
<a href="http://validator.w3.org/check/referer">
Click here to validate.
</a>
</p>

</body>
</html>
